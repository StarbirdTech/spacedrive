apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

detekt {
  config.setFrom("${rootProject.projectDir}/config/detekt.yml")
  buildUponDefaultConfig = true
  allRules = false
  autoCorrect = false
  parallel = true
}

ktlint {
  android = true
  outputToConsole = true
  outputColorName = "RED"
  ignoreFailures = false
  filter {
    exclude("**/generated/**")
    include("**/kotlin/**")
  }
}

android {
  compileSdkVersion 35

  defaultConfig {
    minSdkVersion 24
    targetSdkVersion 35
  }

  sourceSets {
    main {
      jniLibs.srcDirs = ['src/main/jniLibs']
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  namespace "com.spacedrive.core"
}

task buildRustCode(type: Exec) {
  workingDir './build-scripts'
  commandLine './build.sh'
}

tasks.named('preBuild').configure {
  dependsOn buildRustCode
}

dependencies {
  implementation project(':expo-modules-core')
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.25"
  implementation "androidx.documentfile:documentfile:1.0.1"
}
